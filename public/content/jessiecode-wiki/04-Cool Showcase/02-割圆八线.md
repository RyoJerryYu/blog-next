---
created_at: 2025-10-12T13:18:15
updated_at: 2025-10-13T18:41:24
---

从下图可看出，正角对应的割线、切线、弦称为正割、正切、正弦，余角对应的称为余割、余切、余弦。

拖动紫色的点，可观察各三角函数如何随角的大小变化而变化。

```jessiecode
---
grid: false
boundingBox: [-1,2,2,-1]
---
O = point(0,0)<<fixed:true>>;
c = circle(O,1)<<strokeWidth:1>>;
P = glider(0.6,0.8,c)<<withLabel:false>>;
r = segment(O,P)<<visible: false>>;
PX = point(function(){return P.X();}, 0)<<visible:false>>;
PY = point(0, function(){return P.Y();})<<visible:false>>;
xoneP = point(1,0)<<visible: false>>;
yoneP = point(0,1)<<visible: false>>;
theta = angle(xoneP,O,P)<<name: 'θ'>>;

xone = line([1,0], [1,1])<<strokeWidth:1>>;
yone = line([0,1], [1,1])<<strokeWidth:1>>;

tanP = intersection(r, xone)<<name:'正割点',color:'magenta'>>;
cotP = intersection(r, yone)<<name:'余割点',color:'green'>>;

sinL = segment(P, PX)<<name:'正弦',withLabel:true,color:'magenta'>>;
cosL = segment(P, PY)<<name:'余弦',withLabel:true,color:'green'>>;

tanL = segment(tanP, xoneP)<<name: '正切', withLabel: true, color: 'magenta'>>;
cotL = segment(cotP, yoneP)<<name: '余切', withLabel: true, color: 'green'>>;

secL = segment(tanP, O)<<name: '正割',withLabel:true, color:'magenta',dash:1,strokeWidth:4>>;
cscL = segment(cotP, O)<<name: '余割',withLabel:true, color: 'green',dash:2,strokeWidth:4>>;

versinL = segment(PX,xoneP)<<name:'正矢',withLabel:true,color:'magenta'>>;
coversinL = segment(PY,yoneP)<<name:'余矢',withLabel:true,color:'green'>>;
```

